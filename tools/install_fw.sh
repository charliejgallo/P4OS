#!/bin/zsh
#
# Pushes the firmware to the board over wifi, without the cable.
#
#   ./tools/install_fw.sh p4os.local
#   ./tools/install_fw.sh amoledos.local build/amoledos.bin
#
# It sends build/amoledos.bin -the app on its own, NOT the merged image- to
# POST /api/ota, which writes it into the idle slot and points the bootloader
# at it. Then it asks for the restart and waits for the board to come back.
#
# What it does NOT do, on purpose:
#
#   - It does not touch NVS. Your wifi, your language, your watchface and the
#     apps' data survive, which is the whole difference against flashing
#     amoledos-full.bin over USB.
#   - It does not touch the .so files on the card. If the ABI changed, run
#     ./tools/install_apps.sh afterwards; the loader refuses mismatched apps
#     and says so in the log.
#
# The image boots ON TRIAL: if it does not come up, the bootloader goes back to
# the previous one by itself at the next restart. That is why this script waits
# and then checks the version - if the board comes back on the old firmware,
# the new one did not survive.
set -e
ROOT=${0:a:h:h}
HOST=${1:?usage: install_fw.sh <ip-or-name> [image.bin]}
BIN=${2:-$ROOT/build/amoledos.bin}

[[ -f $BIN ]] || { echo "$BIN is missing - run 'idf.py build' first"; exit 1; }

size=$(wc -c < $BIN | tr -d ' ')
echo "== $(basename $BIN), $size B -> $HOST =="

# Resolve the host to an IP and poll THAT afterwards, not the name.
#
# mDNS goes quiet while the board reboots and macOS caches the failure, so
# "amoledos.local" can stay unresolvable for over a minute after the portal is
# already answering. The first run of this script reported a timeout for a
# board that had been up for 5 seconds. The address does not change across a
# reboot -it is the same DHCP lease- so asking curl what it connected to is
# enough.
before=$(curl -sS --max-time 10 "http://$HOST/api/status" || true)
[[ -n $before ]] || { echo "   $HOST does not answer - is it on the network?"; exit 1; }
IP=$(curl -sS -o /dev/null -w '%{remote_ip}' --max-time 10 "http://$HOST/api/status" || true)
[[ -n $IP ]] || IP=$HOST
slot_before=$(print -r -- $before | sed -n 's/.*"slot":"\([^"]*\)".*/\1/p')
echo "   before: $before"
echo "   polling $IP after the restart (mDNS is unreliable right after a reboot)"

code=$(curl -sS -o /tmp/aos_ota_out.$$ -w '%{http_code}' --max-time 300 \
    -X POST "http://$HOST/api/ota" \
    -H 'Content-Type: application/octet-stream' \
    --data-binary "@$BIN") || code=000

if [[ $code != 2* ]]; then
    echo "   FAILED (HTTP $code): $(cat /tmp/aos_ota_out.$$ 2>/dev/null | head -c 300)"
    rm -f /tmp/aos_ota_out.$$
    exit 1
fi
echo "   written: $(cat /tmp/aos_ota_out.$$)"
rm -f /tmp/aos_ota_out.$$

echo "   restarting..."
curl -sS --max-time 10 -X POST "http://$HOST/api/ota/restart" > /dev/null || true

# It takes about 5 s to boot and reach the portal. Give it 60 and say what
# happened, because "it did not answer" and "it went back to the old one" are
# very different things.
for i in {1..30}; do
    sleep 2
    after=$(curl -sS --max-time 4 "http://$IP/api/status" 2>/dev/null || true)
    if [[ -n $after ]]; then
        echo "   back after $((i * 2)) s: $after"
        slot_after=$(print -r -- $after | sed -n 's/.*"slot":"\([^"]*\)".*/\1/p')
        echo
        if [[ -n $slot_before && -n $slot_after ]]; then
            if [[ $slot_before == $slot_after ]]; then
                echo "IT DID NOT TAKE: still running from $slot_after."
                echo "The trial image must have failed and the bootloader went back."
                echo "Look at 'idf.py monitor' for the reason."
                exit 1
            fi
            echo "Running from $slot_after (it was on $slot_before): the update took."
        fi
        echo "The image boots on trial and confirms itself at 30 s of uptime; until"
        echo "then /api/status says \"trial\":true. If it does not survive, the next"
        echo "restart goes back to the previous one on its own."
        exit 0
    fi
done

echo "   it did not answer in 60 s."
echo "   That does not mean it is bricked: the trial image may have failed and"
echo "   the bootloader may be going back to the previous one. Plug the USB in"
echo "   and look at 'idf.py monitor' before reflashing."
exit 1
